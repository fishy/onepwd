load("@rules_kotlin//kotlin:android.bzl", "kt_android_library")
load("@rules_kotlin//kotlin:jvm.bzl", "kt_jvm_library")
load("@rules_kotlin//kotlin:kotlin.bzl", "define_kt_toolchain")
load(
    "@rules_oppia_android//:oppia_android_application.bzl",
    "declare_deployable_application",
    "oppia_android_application",
)

define_kt_toolchain(
    name = "kotlin_toolchain",
    api_version = "1.8",
    jvm_target = "11",
    language_version = "1.8",
)

PACKAGE = "com.yhsif.onepwd"

MANIFEST = "AndroidManifest.xml"

MANIFEST_VALUES = {"PACKAGE": PACKAGE}

declare_deployable_application(
    name = "install",
    aab_target = "//:debug",
)

[
    oppia_android_application(
        name = name,
        config_file = "//:bundle_config.pb.json",
        custom_package = PACKAGE,
        dexopts = ["--force-jumbo"],
        manifest = MANIFEST,
        manifest_values = MANIFEST_VALUES,
        proguard_generate_mapping = False,
        shrink_resources = release,
        deps = [
            ":onepwd",
        ],
    )
    for name, release in (
        ("debug", False),
        ("release", True),
    )
]

HMAC_SRCS = ["src/com/yhsif/onepwd/HmacMd5.kt"]

kt_jvm_library(
    name = "hmac_md5",
    srcs = HMAC_SRCS,
    visibility = [
        "//tests:__pkg__",
    ],
)

kt_android_library(
    name = "onepwd",
    srcs = glob(
        ["src/**/*.kt"],
        exclude = HMAC_SRCS,
    ),
    custom_package = PACKAGE,
    manifest = MANIFEST,
    resource_files = glob(["res/**/*"]),
    deps = [
        ":hmac_md5",
        "@maven//:androidx_appcompat_appcompat",
        "@maven//:androidx_biometric_biometric",
        "@maven//:androidx_cardview_cardview",
        "@maven//:androidx_core_core_ktx",
        "@maven//:androidx_fragment_fragment_ktx",
        "@maven//:androidx_preference_preference",
        "@maven//:androidx_recyclerview_recyclerview",
        "@maven//:androidx_sqlite_sqlite",
        "@maven//:androidx_sqlite_sqlite_framework",
        "@maven//:com_google_android_material_material",
        "@maven//:org_jetbrains_kotlinx_kotlinx_coroutines_android",
    ],
)
